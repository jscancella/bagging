plugins {
  id 'java'
  
  //these have to be in the main project for now see:
  //https://discuss.gradle.org/t/how-do-i-include-buildscript-block-from-external-gradle-script/7016/2
  id "com.github.nbaztec.coveralls-jacoco" version "1.2.20" 
  id "de.aaschmid.cpd" version "3.4"
  id "org.ajoberstar.grgit" version "5.3.0"
  id "com.github.spotbugs" version "6.0.24"
  id 'info.solidsoft.pitest' version '1.15.0'
  id "com.dorongold.task-tree" version "4.0.0"
}
apply from: 'eclipse.gradle'
apply from: 'maven-central.gradle'
apply from: 'code-quality.gradle'
apply from: 'message-bundle.gradle'

sourceCompatibility = 17
targetCompatibility = 17

project.group = "com.github.jscancella"

if(project.version == "unspecified"){
  String now = new Date().format( 'MMM-dd-yyyy_HH-mm-ss' )
  project.version = "1.0.0-${now}-SNAPSHOT"
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.slf4j:slf4j-api:2.0.16'
  implementation 'com.fasterxml.jackson.core:jackson-core:2.18.0'
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.0'
    
  testImplementation 'org.junit.jupiter:junit-jupiter:5.11.2'
  testImplementation 'org.springframework.boot:spring-boot-starter-logging:3.3.4'
  testImplementation 'org.bouncycastle:bcprov-jdk15on:1.70'
  testImplementation 'org.kamranzafar:jtar:2.3'
}

test { 
  useJUnitPlatform()
  testLogging {
    events "passed", "skipped", "failed"
  }
  reports {
    html.required = true
  }
  //testLogging.showStandardStreams = true
}

import org.ajoberstar.grgit.*
task cloneConformanceSuite(){
  group "Verification"
  description "Download the bagit-conformance-suite if it doesn't exist."
  File location = file("${project.projectDir}/bagit-conformance-suite")
  outputs.dir(location)
  
  onlyIf { !location.exists() }
  doLast{
      Grgit.clone(dir: location, 
                  uri: 'https://github.com/libraryofcongress/bagit-conformance-suite.git')
    }
}

task updateConformanceSuite(){
  group "Verification"
  description "Update the bagit-conformance-suite if needed."
  File location = file("${project.projectDir}/bagit-conformance-suite")
  outputs.dir(location)
  
  onlyIf { location.exists() }
  doLast{
      Grgit grgit = Grgit.open(dir: location)
      grgit.pull()
    }
}
