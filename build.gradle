plugins {
  id 'java'
  
  //these have to be in the main project for now see:
  //https://discuss.gradle.org/t/how-do-i-include-buildscript-block-from-external-gradle-script/7016/2
  id "com.github.kt3k.coveralls" version "2.12.0" 
  id "de.aaschmid.cpd" version "3.3"
  id "org.owasp.dependencycheck" version "7.1.1"
  id "org.ajoberstar.grgit" version "5.0.0"
  id "com.github.spotbugs" version "5.0.9"
  id 'info.solidsoft.pitest' version '1.7.4'
  id "com.github.ben-manes.versions" version "0.42.0"
}
apply from: 'eclipse.gradle'
apply from: 'maven-central.gradle'
apply from: 'code-quality.gradle'
apply from: 'message-bundle.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

project.group = "com.github.jscancella"

if(project.version == "unspecified"){
  String now = new Date().format( 'MMM-dd-yyyy_HH-mm-ss' )
  project.version = "1.0.0-${now}-SNAPSHOT"
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.slf4j:slf4j-api:1.7.36'
  implementation 'com.fasterxml.jackson.core:jackson-core:2.13.3'
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.3'
    
  testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
  testImplementation 'org.springframework.boot:spring-boot-starter-logging:2.7.2'
  testImplementation 'org.bouncycastle:bcprov-jdk15on:1.70'
  testImplementation 'org.kamranzafar:jtar:2.3'
}

test { 
  useJUnitPlatform()
  testLogging {
    events "passed", "skipped", "failed"
  }
  reports {
    html.enabled = true
  }
  //testLogging.showStandardStreams = true
}

import org.ajoberstar.grgit.*
task cloneConformanceSuite(){
  group "Verification"
  description "Download the bagit-conformance-suite if it doesn't exist."
  File location = file("${project.projectDir}/bagit-conformance-suite")
  outputs.dir(location)
  
  //TODO figure out how to make sure it always checks it out using unix line endings
  //otherwise you get checksum errors...
  onlyIf { !location.exists() }
  doLast{
      Grgit.clone(dir: location, 
                  uri: 'https://github.com/libraryofcongress/bagit-conformance-suite.git')
    }
}
